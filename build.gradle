/**
 * CurrencyBG App build script
 */
 
buildscript {
	repositories {
		jcenter()
		mavenCentral()
	}
    dependencies {
        classpath 'com.bmuschko:gradle-tomcat-plugin:2.2.4'
    }	
}

apply plugin: 'eclipse'
apply plugin: 'eclipse-wtp'
apply plugin: 'java' 
apply plugin: 'war'
apply plugin: 'com.bmuschko.tomcat'

def tomcatVersion = '7.0.59'

repositories {
	jcenter()
	mavenCentral()
}

eclipse {
	project {
		natures 'org.eclipse.jdt.core.javanature',
				'org.springsource.ide.eclipse.gradle.core.nature',
				'org.eclipse.wst.common.modulecore.ModuleCoreNature',
				'org.eclipse.wst.common.project.facet.core.nature',				
				'org.eclipse.jem.workbench.JavaEMFNature',
				'org.eclipse.wst.jsdt.core.jsNature'
	}
	project.referencedProjects.each { evaluationDependsOn it }
	jdt {
    	sourceCompatibility = JavaVersion.VERSION_1_7
    	targetCompatibility = JavaVersion.VERSION_1_7
    }
  	//classpath.plusConfigurations += [ configurations.compile, configurations.testCompile ]
    classpath {
    	downloadJavadoc = true
    }
}

dependencies {
	compile 'commons-io:commons-io:2.+',
			'commons-lang:commons-lang:2.6',
			'com.google.guava:guava:19.+'
			
    providedCompile 'javax.servlet:javax.servlet-api:3.0.+',
    				'javax.servlet.jsp:jsp-api:2.2'

    testCompile 'junit:junit:4.+'
    
    tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
           "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}",
           "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}"    
}

war {
	manifest {
		def manifestClasspath = configurations.compile.collect { it.getName() }.join(',')
    	attributes (
    		"Class-Path": manifestClasspath,
    		"Implementation-Vendor": 'Vexelon.NET Services',
    		"Implementation-Title": 'CurrencyBG Server',
    		"Implementation-Version": '1.0'
    	)		
	}
}

tomcat {
    httpPort = 8090
    httpsPort = 8091
    enableSSL = false
    contextPath = 'currencybg'
}